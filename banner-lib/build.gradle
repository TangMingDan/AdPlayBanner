apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.+'
    implementation 'com.android.support:support-v4:26.+'

    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'com.facebook.fresco:fresco:1.8.0'
    // 支持 GIF 动图，需要添加
    compile 'com.facebook.fresco:animated-gif:1.8.0'
    // 支持 WebP （静态图+动图），需要添加
    compile 'com.facebook.fresco:animated-webp:1.8.0'
    compile 'com.facebook.fresco:webpsupport:1.8.0'
    // 仅支持 WebP 静态图，需要添加
    compile 'com.facebook.fresco:webpsupport:1.8.0'

    // Glide Start
    compile 'com.github.bumptech.glide:glide:4.0.0-RC0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0-RC0'
    // Glide End
    testImplementation 'junit:junit:4.12'
}


//important
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
    }
}

version = "0.6"//发布的library的版本号,这个比较关键！！！

def siteUrl = 'https://github.com/ryanlijianchang/AdPlayBanner'      // 项目的主页(可随意，建议填library的github地址)
def gitUrl = 'https://github.com/ryanlijianchang/AdPlayBanner'      // Git仓库的url(可随意，建议填library的github地址)
group = "com.ryane.banner"    // 这里是groupId ,必须填写(建议填唯一的包名)

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                // Add your description here
                name 'The library is a custom view about banner'     //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'ryanlijianchang'        //开发者的基本信息(建议填自己名称)
                        name 'RyanLee'    //开发者基本信息(建议填自己名称)
                        email 'liji.anchang@163.com'   //开发者基本信息(建议填自己名称)
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}

//important 读取 local.properties 文件里面的 bintray.user和bintray.apikey
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user") //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        group = group
        name = "AdPlayBanner"//发布到JCenter上的项目名字，必须填写
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}



